const OBJ_EVENT_ID_PENGUIN_DND = 7
const OBJ_EVENT_ID_PENGUIN = 8

mapscripts MudskipperPoint_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(MUDSKIPPER_POINT_STATE)
        call(PENGUIN_EVENT)
        call(RANDOM_EVENT_SPAWNS_MUDSKIPPER_POINT)
    }
}

script RANDOM_EVENT_SPAWNS_MUDSKIPPER_POINT {
	random(8)
	if ((var(VAR_RESULT) == 1)) {
		clearflag(FLAG_TEMP_5)
		addobject(14)
	}
	else {
		setflag(FLAG_TEMP_5)
		removeobject(14)
	}
    random(8)
	if ((var(VAR_RESULT) == 0)) {
		clearflag(FLAG_TEMP_4)
		addobject(13)
	}
	else {
		setflag(FLAG_TEMP_4)
		removeobject(13)
	}
	return
}

script MUDSKIPPER_POINT_STATE {
    setflag(FLAG_TEMP_F)
    removeobject(9)
    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 155) {
        clearflag(FLAG_TEMP_F)
        addobject(9)
        setvar(VAR_TEMP_5, 1)
    }
    return
}

script NPC_STORY_NPC_MUDSKIPPER_POINT_SIRTHEODORE {
    faceplayer
    goto(STORY_NPC_MUDSKIPPER_POINT_SIRTHEODORE)
}
script TRIGGER_STORY_NPC_MUDSKIPPER_POINT_SIRTHEODORE {
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    switch (var(VAR_TEMP_2)){ 
		case 18: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_1)
            waitmovement(0)
            applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACEDOWN)
        case 19: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_2)
            waitmovement(0)
            applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACEDOWN)
        case 20: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_3)
            waitmovement(0)
            applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACERIGHT)
        case 21: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_4)
        case 22: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_5)
        case 23: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_6)
            waitmovement(0)
            applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACERIGHT)
        case 24: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_7)
        case 25: 
            applymovement(9, MOVEMENT_MUDSKIPPER_THEO_8)
            waitmovement(0)
            applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACERIGHT)
    }
    waitmovement(0)
    goto(STORY_NPC_MUDSKIPPER_POINT_SIRTHEODORE)
}

movement MOVEMENT_MUDSKIPPER_THEO_1 {
    walk_up * 2
    step_end
}
movement MOVEMENT_MUDSKIPPER_THEO_2 {
    walk_up * 1
    step_end
}
movement MOVEMENT_MUDSKIPPER_THEO_3 {
    walk_up * 1
    face_left
    step_end
}
movement MOVEMENT_MUDSKIPPER_THEO_4 {
    face_left
    step_end
}
movement MOVEMENT_MUDSKIPPER_THEO_5 {
    walk_down
    face_left
    step_end
}
movement MOVEMENT_MUDSKIPPER_THEO_6 {
    walk_down * 2
    walk_left
    step_end
}
movement MOVEMENT_MUDSKIPPER_THEO_7 {
    walk_down * 3
    walk_left
    step_end
}
movement MOVEMENT_MUDSKIPPER_THEO_8 {
    walk_down * 4
    walk_left * 2
    step_end
}

script STORY_NPC_MUDSKIPPER_POINT_SIRTHEODORE {
    lockall
    if (var(VAR_TEMP_5) == 2) {
        namebox ("Sir Theodore")
        msgbox(format("Did you change your mind?\pWould you like to travel to Falador with me?"), MSGBOX_YESNO)
        if (var(VAR_RESULT)) {
            msgbox(format("Alright, to Falador with great haste!"))
            closemessage
            hidenamebox
            warp(MAP_FALADOR_CASTLE_3F, 3)
            waitstate
        }
        closemessage
        hidenamebox
        release
        end
    }
    namebox ("Sir Theodore")
    msgbox(format("{PLAYER}?!\pWow is that really you?!\pWe were worried when we lost contact with you and Ned.\pSpeaking of which, where is Ned?"))
    hidenamebox
    closemessage
    delay(30)
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_1) == 18) {
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_INTERACT_LEFT)
    }
    elif ((var(VAR_TEMP_2) == 18) || (var(VAR_TEMP_2) == 19)) {
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_INTERACT_DOWN)
    }
    else {
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_INTERACT_RIGHT)
    }
    waitmovement(0)
    namebox ("Sir Theodore")
    msgbox(format("…{PAUSE 15}…{PAUSE 15}…{PAUSE 15}…{PAUSE 15}…{PAUSE 15}…{PAUSE 15}"))
    hidenamebox
    closemessage
    playse(SE_LEDGE)
    applymovement(9, MOVEMENT_EXCLAMATIONMARK_JUMP_LOCK)
    namebox ("Sir Theodore")
    msgbox(format("WHAT!?\pYOU JUST LEFT HIM ON CRANDOR?!"))
    waitmovement(0)
    msgbox(format("We must report back to Sir Amik Varze in Falador right away and organise a rescue mission!!"))
    hidenamebox
    closemessage
    setvar(VAR_TEMP_5, 2)
    namebox ("Sir Theodore")
    msgbox(format("Would you like to travel to Falador with me?"), MSGBOX_YESNO)
    if (var(VAR_RESULT)) {
        msgbox(format("Alright, to Falador with great haste!"))
        closemessage
        hidenamebox
        warp(MAP_FALADOR_CASTLE_3F, 3)
        waitstate
    }
    closemessage
    hidenamebox
    releaseall
    end
}

movement MOVEMENT_EXCLAMATIONMARK_JUMP_LOCK {
    emote_exclamation_mark
    lock_facing_direction
    jump_in_place_down
    unlock_facing_direction
    step_end
}


script ENCOUNTER_PENGUIN_BARREL_MUDSKIPPER_POINT {
	lock
	faceplayer
    namebox ("Penguin")
	playmoncry(SPECIES_PENGUIN_DISGUISE_BOX_FORM, 0)
	msgbox(format("Noot! Noot!"))
	waitmoncry
    hidenamebox
	closemessage
	delay(1)
	setwildbattle (SPECIES_PENGUIN_DISGUISE_BOX_FORM, 30, ITEM_NONE)
	dowildbattle
    if (var(VAR_RESULT) == B_OUTCOME_WON || var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
        fadescreenswapbuffers(FADE_TO_BLACK)
        setflag(FLAG_PENGUIN_HUNT_ON_ROUTE)
        removeobject(OBJ_EVENT_ID_PENGUIN_DND)
        removeobject(OBJ_EVENT_ID_PENGUIN)
		savebgm(MUS_DUMMY) //<- IMPORTANT
        fadedefaultbgm //<- IMPORTANT
        fadescreenswapbuffers(FADE_FROM_BLACK)
        closemessage
	}
    end
}


script SIGN_MudskipperPoint {
    msgbox(format("Mudskipper Point.\nBeware of Mudskippers."))
    closemessage
    end
}