const DEMONHEADGE_ID = 24
const JMOD_1_ID = 25
const JMOD_2_ID = 26
const DEACON_ID = 3
const FROGEEL_ID = 29
const FROGEEL_2_ID = 30
const HYPERSTAN_ID = 27
const ORCHY_ID = 28
const ZANIK_ID = 22
const MITHRILMAN_ID = 31
const GNOMEBOY_ID = 32

mapscripts HAM_Hideout_B1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(HAMHIDEOUT_B1F_STATE)
        goto(HAM_HIDEOUT_GOLDENGNOME_PLACENPCS)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_POKESCAPE_GOLDENGNOMES, 10: HAM_HIDEOUT_GOLDENGNOME_STATE
    ]
}

script HAMHIDEOUT_B1F_STATE {
    clearflag(FLAG_TEMP_D) //SHOWS NPCS
    setflag(FLAG_TEMP_C) //HIDES TRAINERS
    setflag(FLAG_TEMP_E)

    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 220) { 
        setvar(VAR_TEMP_0, 1)
    }
    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 225) { 
        setvar(VAR_TEMP_0, 2)
    }
    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 235) { 
        setflag(FLAG_TEMP_D) //Hides NPCS
        clearflag(FLAG_TEMP_C) //Shows TRAINERS
        clearflag(FLAG_TEMP_E)
    }
    return
}



script HAM_HIDEOUT_GOLDENGNOME_PLACENPCS {
    if (var(VAR_POKESCAPE_GOLDENGNOMES) == 5) 
        {
            addobject(DEMONHEADGE_ID)
            addobject(JMOD_1_ID)
            addobject(JMOD_2_ID)
            setobjectxyperm(DEMONHEADGE_ID,27,20)
            setobjectxyperm(JMOD_1_ID,27,20)
            setobjectxyperm(JMOD_2_ID,28,20)

            addobject(FROGEEL_ID)
            setobjectxyperm(FROGEEL_ID,27,20)
            addobject(FROGEEL_2_ID)
            setobjectxyperm(FROGEEL_2_ID,11,23)

            addobject(HYPERSTAN_ID)
            setobjectxyperm(HYPERSTAN_ID,27,20)
            addobject(ORCHY_ID)
            setobjectxyperm(ORCHY_ID,27,20)


            addobject(ZANIK_ID)
            setobjectxyperm(ZANIK_ID,16,23)
            setobjectmovementtype(ZANIK_ID,MOVEMENT_TYPE_FACE_UP)
            addobject(MITHRILMAN_ID)
            setobjectxyperm(MITHRILMAN_ID,22,23)
            setobjectmovementtype(MITHRILMAN_ID,MOVEMENT_TYPE_FACE_UP)
            addobject(GNOMEBOY_ID)
            setobjectxyperm(GNOMEBOY_ID,22,21)
            setobjectmovementtype(GNOMEBOY_ID,MOVEMENT_TYPE_FACE_UP)

            removeobject(14)
            removeobject(4)
            removeobject(6)
            removeobject(5)
            removeobject(8)
            setflag(FLAG_TEMP_1)
            setvar(VAR_POKESCAPE_GOLDENGNOMES, 10)
        }
    
}


script HAM_HIDEOUT_GOLDENGNOME_STATE {
        applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACEUP)
        waitmovement(0)
        

        call(HAM_HIDEOUT_GOLDENGNOME_EVENT)
        //setvar (VAR_TEMP_0, 1)
		return
}

script GOLDENGNOME_CAMERA_CALL {
    lock
    //---SPAWN CAMERA EVENT---
    delay(60)
    special(SpawnCameraObject) 
    return
}

//EVENT
script HAM_HIDEOUT_GOLDENGNOME_EVENT {
    fadescreenswapbuffers(FADE_TO_BLACK)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACELEFT)
    applymovement(ZANIK_ID, Movement_InteractFACERIGHT)
    delay(60)
    call(GOLDENGNOME_CAMERA_CALL)
    applymovement(OBJ_EVENT_ID_CAMERA, GOLDENGNOME_CAMERA_1_START)
    delay(60)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(60)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACEUP)
    applymovement(ZANIK_ID, Movement_InteractFACEUP)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    delay(60)

    namebox("H.A.M Deacon")
    msgbox(format("And thus, we will finally execute our plan to rid this world of those fiendish Monsters and send them to the Abyss where they belong!"))
    waitmessage
    closemessage
    hidenamebox
    playbgm (MUS_PS_ROUTE_LUMBRIDGE, 1)
    applymovement(JMOD_1_ID, GOLDENGNOME_MOVEMENT_1)
    waitmovement(0)
    applymovement(DEACON_ID, Movement_InteractFACERIGHT)
    waitmovement(0)
    namebox("H.A.M Deacon")
    msgbox(format("…{PAUSE 16}…{PAUSE 16}…\pWhat is this?"))
    waitmessage
    closemessage
    hidenamebox
    applymovement(JMOD_1_ID, Movement_InteractFACELEFT)
    waitmovement(0)
    //JMOD go get demon
    applymovement(DEACON_ID, Movement_InteractFACEDOWN)
    applymovement(JMOD_1_ID, GOLDENGNOME_MOVEMENT_5)
    namebox("H.A.M Deacon")
    msgbox(format("Sorry everyone, it seems like there is some sort of an announcement."))
    waitmessage
    closemessage
    hidenamebox
    //QUESTION MARKS FROM HAM
    waitmovement(0)
    delay(10)
    applymovement(JMOD_1_ID, GOLDENGNOME_MOVEMENT_6)
    delay(20)
    applymovement(DEMONHEADGE_ID, GOLDENGNOME_MOVEMENT_2)
    delay(10)
    applymovement(JMOD_2_ID, GOLDENGNOME_MOVEMENT_4)
    delay(60)
    applymovement(DEACON_ID, GOLDENGNOME_MOVEMENT_3)
    waitmovement(JMOD_2_ID)
    delay(10)
    namebox("Demonheadge")
    msgbox(format("Hello everyone.\pI am Demonheadge, the creator of PokeScape.\pAnd as of a few seconds ago I have been informed that PokeScape has been nominated for a Golden Gnome!"))
    waitmessage
    closemessage
    hidenamebox
    //SURPREISED FACES
    playbgm (MUS_PS_PORT_SARIM, 1)
    call(FROGEEL_EXCITED)

    namebox("Demonheadge")
    msgbox(format("That's crazy right?!\pI've also been asked to say a few words."))
    waitmessage
    msgbox(format("I would like to thank all of the incredible people who helped and assisted in some way on this project.\pWithout them it wouldn't be where it is today."))
    closemessage
    hidenamebox
    applymovement(HYPERSTAN_ID, GOLDENGNOME_MOVEMENT_GUEST)
    waitmovement(0)
    namebox("Hyper Stan")
    msgbox(format("Hey, I am HyperStan.\pI drew most of the Pixel Art for this Project.\p…{PAUSE 16} Using this moment I would like to say that Ja-"))
    closemessage
    hidenamebox
    applymovement(JMOD_2_ID, GOLDENGNOME_MOVEMENT_JMOD_WALK_FORWARD)
    waitmovement(0)
    namebox("Jagex Mod")
    msgbox(format("Sorry, Hyper Stan has just been muted for inappropriate behaviour.\pPlease continue the awards."))
    closemessage
    hidenamebox
    applymovement(JMOD_2_ID, GOLDENGNOME_MOVEMENT_JMOD_WALK_BACKWARDS)
    waitmovement(0)

    applymovement(HYPERSTAN_ID, GOLDENGNOME_MOVEMENT_GUEST_stan)
    waitmovement(0)

    applymovement(ORCHY_ID, GOLDENGNOME_MOVEMENT_GUEST)
    waitmovement(0)
    namebox("Orchy")
    msgbox(format("Hello I am known as RuneScape ReOrchestrated.\pI made all of the custom music for this Project.\pI hope you enjoy it!"))
    closemessage
    hidenamebox
    applymovement(ORCHY_ID, GOLDENGNOME_MOVEMENT_GUEST_orchy)
    waitmovement(0)

    applymovement(FROGEEL_2_ID, GOLDENGNOME_MOVEMENT_GUEST_frogeel)
    waitmovement(0)
    namebox("FROGEEL")
    msgbox(format("AAAAAAAAAAAA!"))
    closemessage
    hidenamebox
    delay(60)
    namebox("Demonheadge")
    msgbox(format("Ah, cannot forget about Frogeel for his pursuit-less despair and endeavor which has kept us motivated to keep going."))
    waitmessage
    closemessage
    hidenamebox
    applymovement(FROGEEL_2_ID, GOLDENGNOME_MOVEMENT_GUEST_frogeel_exit)
    waitmovement(0)
    namebox("Demonheadge")
    msgbox(format("Some other mentions are ImcandoBrett, Ryu Saint, Lopendebank, Chase, MrKalius, Sudo Bash, Jitterbug, GentleShen, PippinSmith and many, many others who have popped in along the way to contribute in one way or another."))
    waitmessage
    msgbox(format("PokeScape has been a passion project on and off for a few years now just for fun and I hope to continue it!\pThank you all for voting for us.\pWe appreciate it a lot.\pNow enjoy the rest of the Golden Gnome Awards!"))
    closemessage
    hidenamebox
    //fadescreen(FADE_TO_BLACK)
    savebgm(MUS_DUMMY)
    fadedefaultbgm
    setvar(VAR_POKESCAPE_GOLDENGNOMES, 15)
    warpwhitefade( MAP_DRAYNOR, 255, 14, 28)
    release
    end
}









movement GOLDENGNOME_CAMERA_1_START{ // camera
    //walk_slow_right * 5
    walk_slow_right * 2
    walk_slow_up * 5
	step_end
}
movement GOLDENGNOME_MOVEMENT_1{ // JMOD 1
    walk_left * 6
    walk_up * 4
    walk_left * 1
	step_end
}
movement GOLDENGNOME_MOVEMENT_2{ // DEMON
    walk_left * 6
    walk_up * 4
    walk_left * 2
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_3{ //DEACON MOVE
    walk_left * 3
    face_right
	step_end
}
movement GOLDENGNOME_MOVEMENT_4{ //JMOD 2
    walk_left * 7
    walk_up * 4
    walk_left * 1
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_5{ //JMOD 1 walk back
    walk_right * 1
    walk_down * 4
    walk_right * 6
	step_end
}
movement GOLDENGNOME_MOVEMENT_6{ // JMOD 1 reenter stage
    walk_left * 6
    walk_up * 4
    walk_left * 3
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_GUEST{ // annouce self
    walk_left * 2
    walk_up * 1
    walk_left * 6
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_GUEST_stan{ // stan move
    walk_left * 1
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_GUEST_orchy{ // orchy move
    walk_right * 1
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_GUEST_frogeel{ // frogeel
    jump_left * 9
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_GUEST_frogeel_exit{ // frogeel
    jump_left * 4
    jump_down * 5
    face_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_JMOD_WALK_FORWARD{ // jmod
    walk_down
	step_end
}
movement GOLDENGNOME_MOVEMENT_JMOD_WALK_BACKWARDS{ // jmod
    lock_facing_direction
    walk_up
	step_end
}


script FROGEEL_EXCITED {
    
    applymovement(FROGEEL_ID, GOLDENGNOME_MOVEMENT_Frogeel)
    applymovement(FROGEEL_2_ID, GOLDENGNOME_MOVEMENT_Frogeel_2)
    waitmovement(0)
}

movement GOLDENGNOME_MOVEMENT_Frogeel{ // frogeel
    jump_left * 8
    jump_up * 1
    jump_left
    jump_right * 2
    jump_left
    jump_2_down * 4
    jump_down * 1
    //jump_left
    //jump_special_down
    //jump_in_place_right_left
	step_end
}

movement GOLDENGNOME_MOVEMENT_Frogeel_2{ // frogeel
    jump_right * 3
    jump_2_up * 1
    jump_up * 1
    jump_right * 2
    jump_2_right * 4
    jump_right * 4
    //jump_left
    //jump_special_down
    //jump_in_place_right_left
	step_end
}












//GOLDEN GNOMES


script GOLDENGNOME_EVENT_START {
    lock
    faceplayer
    if (var(VAR_POKESCAPE_GOLDENGNOMES)==0)
    {
        msgbox(format("Would you like to watch the Golden Gnomes Awards?"), MSGBOX_YESNO)
        if (var(VAR_RESULT)==1)
        {
            setvar(VAR_POKESCAPE_GOLDENGNOMES, 5)
            msgbox(format("Enjoy watching!"))
            waitmessage
            warpwhitefade( MAP_HAM_HIDEOUT_B1F, 255, 17, 23)
            end
        }
    }
    msgbox(format("I hope you enjoyed watching Golden Gnomes."))
    release
    end
}


script GOLDENGNOME_EVENT_END {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACELEFT)
    applymovement(1, Movement_InteractFACERIGHT)
    waitmovement(0)
    msgbox(format("I hope you enjoyed watching the Golden Gnomes.\pHere is a gift for you."))
    waitmessage
    closemessage
    call(GiveMon_GOLDENGNOME)
    setvar(VAR_POKESCAPE_GOLDENGNOMES, 20)
    //setflag(FLAG_GOLDENGNOME)
    release
    end
}















script GOLDENGNOME_TEST {
    if (flag(FLAG_GOLDENGNOME) == TRUE) {
        
        //setobjectxyperm(OBJ_EVENT_ID_PLAYER,19,30)
        //setobjectxyperm(OBJ_EVENT_ID_CAMERA,19,30)
        //call(GOLDENGNOME_EVENT_PART1)
        //applymovement(OBJ_EVENT_ID_CAMERA, GOLDENGNOME_CAMERA_1_START)
        //waitmovement(0)
        //applymovement(OBJ_EVENT_ID_PLAYER, GOLDENGNOME_MOVEMENT_2)
        //waitmovement(0)
        setflag (FLAG_HIDE_MAP_NAME_POPUP)
        msgbox(format("Testing…"))
        closemessage
        end

    }
    return
}











//HAM HIDEOUT EVENT


script TRIGGER_HAMHIDEOUT_EVENT_1 {
    lock
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)     
    if (var(VAR_TEMP_1) == 11){                 
        applymovement(OBJ_EVENT_ID_FOLLOWER, HAM_Hideout_Zanik_1_Right)         //have Zanik move beside player
        waitmovement(0)
        applymovement(33, HAM_Hideout_Intro_1_Left)         //have HAM member move to speak to them
        waitmovement(0)
    } 
    else {
        applymovement(OBJ_EVENT_ID_FOLLOWER, HAM_Hideout_Zanik_1_Left)         //have Zanik move beside player
        waitmovement(0)
        applymovement(33, HAM_Hideout_Intro_1_Right)        //have HAM member move to speak to them
        waitmovement(0)
    }
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACEUP)
    applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_InteractFACEUP)
    waitmovement(0)
    namebox("HAM Grunt")
    msgbox(format("You're late!\lYou should have come in to cover the next shift hours ago.\p...{PAUSE 15}\pI don't want to hear any excuses, now go to the jail!\lSomeone needs to keep watch over our guests!\p...{PAUSE 15}\pWhat's with the blank expressions?\lSurely you both know it's in the south-east of the cave by now, right!?"))
    hidenamebox
    closemessage
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)  
    if (var(VAR_TEMP_1) == 11){                 
        applymovement(33, HAM_Hideout_Intro_2_Left)         //have HAM member move to original position
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACERIGHT)
        applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_InteractFACELEFT)
        waitmovement(0)
    } 
    else {
        applymovement(33, HAM_Hideout_Intro_2_Right)        //have HAM member move to original position
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACELEFT)
        applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_InteractFACERIGHT)
        waitmovement(0)
    }
    namebox("Zanik")
    msgbox(format("Did you hear that {PLAYER}?! This gives us the perfect cover to check whether H.A.M have the Prince in their jail cells.\lLet's go!"))
    closemessage
    hidenamebox
    setvar(VAR_TEMP_0, 2)
    setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 225)
    release
    end
}

//Movement scripts for trigger 1

movement HAM_Hideout_Zanik_1_Left {
    walk_left
    walk_down
    step_end
}

movement HAM_Hideout_Zanik_1_Right {
    walk_right
    walk_down
    step_end
}

movement HAM_Hideout_Intro_1_Left {
    walk_left * 2
    face_down
    emote_double_exclamation_mark
    walk_down
    step_end
}

movement HAM_Hideout_Intro_1_Right {
    walk_left
    face_down
    emote_double_exclamation_mark
    walk_down
    step_end
}

movement HAM_Hideout_Intro_2_Left {
    walk_up
    walk_right * 2
    face_original_direction
    step_end
}

movement HAM_Hideout_Intro_2_Right {
    walk_up
    walk_right
    face_original_direction
    step_end
}

script TRIGGER_HAMHIDEOUT_EVENT_2 {
    lock
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)     
    if (var(VAR_TEMP_1) == 30){                 
        applymovement(OBJ_EVENT_ID_PLAYER, HAM_Hideout_Player_2_Left)  //walk into place
        applymovement(OBJ_EVENT_ID_FOLLOWER, HAM_Hideout_Zanik_2_Left) //have Zanik move beside player
        waitmovement(0)
    } 
    else {
        applymovement(OBJ_EVENT_ID_PLAYER, HAM_Hideout_Player_2_Right)  //walk into place
        applymovement(OBJ_EVENT_ID_FOLLOWER, HAM_Hideout_Zanik_2_Right) //have Zanik move beside player        
        waitmovement(0)
    }
    applymovement(18, HAM_Hideout_Jail_Guard_Top_1)     //top  guard
    applymovement(19, HAM_Hideout_Jail_Guard_Bottom_1)  //bottom guard
    waitmovement(0)
    namebox("H.A.M Guard")
    msgbox(format("You two should have been here ages ago!\lWhat kept you?"))
    applymovement(19, MOVEMENT_EXCLAMATIONMARK_JUMP_LEFT)  //bottom guard
    waitmovement(0)
    msgbox(format("It doesn't matter, you're here now.\lMake sure you guard the prisoners!\lDon't let them out of your sight!"))
    hidenamebox
    closemessage
    applymovement(18, HAM_Hideout_Jail_Guard_Top_2)     //top  guard exits
    applymovement(19, HAM_Hideout_Jail_Guard_Bottom_2)  //bottom guard exits
    waitmovement(0)
    removeobject(18)
    removeobject(19)
    setflag(FLAG_TEMP_6)                                      //Check this is a useable flag. Also add in some mapscripts to set/hide flags and NPCs
    setvar(VAR_TEMP_0, 3)
    setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 230)
    release
    end
}

//Movement Scripts for Trigger 2

movement HAM_Hideout_Player_2_Left {
    walk_slow_down * 2 
    walk_slow_right
    step_end
}

movement HAM_Hideout_Player_2_Right {
    walk_slow_down * 3 
    face_right
    step_end
}

movement HAM_Hideout_Zanik_2_Left {
    walk_slow_down * 3 
    walk_slow_right
    step_end
}

movement HAM_Hideout_Zanik_2_Right {
    walk_slow_down * 2
    face_right
    step_end
}

movement HAM_Hideout_Jail_Guard_Top_1 {
    walk_fast_down * 2
    walk_fast_left
    step_end
}

movement HAM_Hideout_Jail_Guard_Bottom_1 {
    walk_fast_up * 2
    walk_fast_right
    step_end
}

movement HAM_Hideout_Jail_Guard_Top_2 {
    walk_up
    walk_left
    walk_up * 2
    walk_left
    walk_up * 2
    walk_left
    walk_up
    walk_left
    walk_up
    walk_left
    walk_up * 2
    step_end
}

movement HAM_Hideout_Jail_Guard_Bottom_2 {
    walk_down
    walk_left * 2
    walk_up * 4
    walk_left
    walk_up * 2
    walk_left
    walk_up
    walk_left
    walk_up * 4
    step_end
}

script TRIGGER_HAMHIDEOUT_EVENT_3 {                              //This needs some polish to work properly with the trigger
    lock
    applymovement(10, Movement_InteractFACEPLAYER)
    applymovement(11, Movement_InteractFACEPLAYER)
    waitmovement(0)
    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 225) { 
        namebox("H.A.M Guard")
        msgbox(format("Shouldn't you two be guarding the prisoners?\lThe jail cell is over to the east, remember?"))
        closemessage
        hidenamebox
        applymovement (OBJ_EVENT_ID_FOLLOWER, MOVEMENT_LOCKFACING_WALK_DOWN)
		applymovement (OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_DOWN)
        waitmovement(0)
        applymovement (10, FACE_ORIGINAL_DIRECTION)                 //Right guard
		applymovement (11, FACE_ORIGINAL_DIRECTION)                 //Left guard
        waitmovement(0)
        release
        end
    }
    elif (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 230) { 
        namebox("H.A.M Guard")
        msgbox(format("Get back to guarding the prisoners!\lJohanhus won't be happy if he hears you've abandoned your posts."))
        closemessage
        hidenamebox
        applymovement (OBJ_EVENT_ID_FOLLOWER, MOVEMENT_LOCKFACING_WALK_DOWN)
		applymovement (OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_DOWN)
        waitmovement(0)
        applymovement (10, FACE_ORIGINAL_DIRECTION)                 //Right guard
		applymovement (11, FACE_ORIGINAL_DIRECTION)                 //Left guard
        waitmovement(0)
        release
        end
    }
    elif (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 235) {           //After the disguises have been removed, battle the guards to pass through
        namebox("H.A.M Guard")
        msgbox(format("Hey, you two aren't H.A.M members!"))
        trainerbattle_two_trainers(TRAINER_HAM_HIDEOUT_GUARD_1, msgbox(format("H.A.M Guard: Johanhus and Sigmund won't be happy about this...")), TRAINER_HAM_HIDEOUT_GUARD_2, msgbox(format("H.A.M Guard: Especially since one of you is a blasted goblin!")))
        namebox("H.A.M Guard")
        msgbox(format("Quick, let's try and find some back-up!\pDon't you dare go in and disturb Johanhus and Sigmund while we're gone!"))
        hidenamebox
        closemessage
        playse(SE_FLEE)
        applymovement (10, HAM_Hideout_Guard_2_Right)
		applymovement (11, HAM_Hideout_Guard_1_Left)
        waitmovement(0)
        waitse
        removeobject(10)
        removeobject(11)
        setflag(FLAG_TEMP_5)                                      //Check this is a useable flag. Also add in some mapscripts to set/hide flags and NPCs
        setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 240)
        release
        end
    }
    else {                                                        //Check what to do if I don't want anything to trigger
    namebox("H.A.M Guard")
    msgbox(format("Sorry, this area is off-limits."))
    hidenamebox
    closemessage
    applymovement (OBJ_EVENT_ID_FOLLOWER, MOVEMENT_LOCKFACING_WALK_DOWN)
	applymovement (OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_DOWN)
    waitmovement(0)
    release
    end
    }
release
end
}

//Movement Scripts for Trigger 3

movement HAM_Hideout_Guard_1_Left {
    walk_fast_down * 2
    walk_fast_right * 4
    walk_fast_up
    walk_fast_right * 2
    walk_fast_up
    walk_fast_right * 2
    walk_fast_up * 6
    face_original_direction
    step_end
}

movement HAM_Hideout_Guard_2_Right {
    walk_fast_right * 5
    walk_fast_up * 2
    walk_fast_right
    walk_fast_up * 4
    step_end
}

//NPCs

script NPC_HAM_HIDEOUT_B1F_1
{
	lock
    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 225) { 
        faceplayer
        namebox ("H.A.M Member")
	    msgbox(format("What did I tell you two?\pGo and guard the jail!\lOtherwise you won't be getting any rations tonight."))
        closemessage
        hidenamebox
        return
    }
    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 230) { 
        faceplayer
        namebox ("H.A.M Guard")
	    msgbox(format("I saw the other guard you relieved walk past not too long ago.\lHave you idiots left the jail unguarded?\pWhat if someone tries to sneak in and break the prisoners out!\lGet back to your post before I have to involved Sigmund!"))
        closemessage
        hidenamebox
        return
    }
    else {
        msgbox(format("The guard is too busy to pay attention to you."))
        closemessage
        hidenamebox
        return
    }
    release
    end
}

script NPC_HAM_HIDEOUT_B1F_2
{
	lock
	faceplayer
    namebox ("H.A.M Member")
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
            msgbox(format("Be vigilant brother, we cannot let the monster scourge pollute our haven."))
    } 
    else {
        msgbox(format("Be vigilant sister, we cannot let the monster scourge pollute our haven."))
    }
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_3
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("Have you had the chance to hear the Deacon speak yet?\lHis words are so wise and inspiring, it's almost as if Saradomin is speaking through him!"))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_4
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("Before I joined H.A.M my family was killed by goblins.\pNow I feel like I have found my life's calling right here."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_5
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("I quite like it here honestly.\pOne thing bugs me however.\pWhy did the leaders decide on the robes being pink?\pIs there some symbolism I am not getting?"))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_6
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("Don't tell any of the other grunts, but I got a thing for Babe.\pDo you know anything that might help me get closer to her?\nThere is a limit to how much hatred against monsters can bring people together…"))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_7
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("We are going to make Lumbridge great again.\nThen the world will follow."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_8
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("If you do not agree with my opinion you are my enemy, that is how the world works."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_9
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("We might use monsters to fight monsters, but we have standards.\pDogs, spiders, cows, pigs, sheep, and cats have been friends and allies to humans for centuries.\nNow they stand with us against the scourge that are monsters!"))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_10
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("Johanhus is an idealist, he wants to solve conflict with words alone.\pSo far it hasn't really worked, though."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_11
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("Sigmund is an extremist among us.\pHe doesn't mind flooding a cavern of goblins if it means they will all die.\nPersonally I get that."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_12
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("Sigmund believes power and might makes right, so we in H.A.M will become the most powerful organisation in Gielinor."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_13
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("We will not tolerate anyone with an opinion differing from our own.\pThat is the path of disaster."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_14
{
	lock
    namebox ("H.A.M Deacon")
    random(10)
    switch (var(VAR_RESULT)) { 
        case 0: 
            msgbox(format("We are the righteous ones in his eyes alone."))
            hidenamebox
		    release
		    end
        case 1:
            msgbox(format("Let them not infest our cities and towns..."))
            hidenamebox
		    release
		    end
        case 2:
            msgbox(format("And lo, we become the power, indeed the force to stop these monsters in their tracks."))
            hidenamebox
		    release
		    end
        case 3:
            msgbox(format("Verily I urge you, my friends to take up your spades and farm your farms to feed our people in this blessed sanctuary."))
            hidenamebox
		    release
		    end
        case 4:
            msgbox(format("If thine monsters visage does frighten thee, then tear it off I say... tear it off!"))
            hidenamebox
		    release
		    end
        case 5:
            msgbox(format("For Saradomin will guide our sword arms and smash the enemies of humans till their bones become dust."))
            hidenamebox
		    release
		    end
        case 6:
            msgbox(format("And let us smite these monsters unto their deaths."))
            hidenamebox
		    release
		    end
        case 7:
            msgbox(format("For they are not the chosen ones in Saradomin's eyes."))
            hidenamebox
		    release
		    end
        case 8:
            msgbox(format("Where do we go for safety from these monsters... here, with our brethren!"))
            hidenamebox
		    release
		    end
        case 9:
            msgbox(format("And let there be no cave or shelter for their spawn until the end of days."))
            hidenamebox
		    release
		    end
    }
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_15
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("Don't forget, through here are the sleeping quarters if you need to take a rest.\lJust make sure not to get caught sleeping if you're on duty, or Sigmund will have your head!"))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_16
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("It's really inspiring seeing such strong female figures like Babe and Patty having positions of authority here.\lThat's how I know this group truly are guided by Saradomin himself.\pThe churches may preach about him, but you never seen any female priests or monks..!"))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_17
{
	lock
	faceplayer
    namebox ("H.A.M Member")
	msgbox(format("It's my turn to prepare tonight's meal.\pThankfully we have plenty of 'taters.\lYou know, po-tay-toes!\pBoil 'em, mash 'em, stick 'em in a stew.\lYum!"))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_18
{
	lock
	faceplayer
    namebox("H.A.M Guard")
    msgbox(format("Sorry, this area is off-limits to regular members."))
    closemessage
    hidenamebox
	release
	end
}

script NPC_HAM_HIDEOUT_B1F_19
{
	lock
	faceplayer
    namebox("H.A.M Guard")
    msgbox(format("Sorry, this area is off-limits.\lSigmund and Johanhus are having an important meeting."))
    closemessage
    hidenamebox
    applymovement(10, FACE_ORIGINAL_DIRECTION)        //The other guard can only be talked to from one direction which is why this movement isn't on both
	release
	end
}

script HAM_HIDEOUT_RANGE
{
	msgbox(format("It's a range for cooking on.\lThough you are not really sure how H.A.M managed to get one working this far underground."))
    closemessage
	release
	end
}